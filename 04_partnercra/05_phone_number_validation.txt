*** Settings ***
Documentation   Test phone number validation when adding new organization
Suite Setup    Login as partner with cra 
Suite Teardown    Log out and Close Browsers
Test Template    Add company
Resource    ../common/assets/secrets.txt
Resource    ../common/assets/resource.txt
Resource    ../local_resource.txt

*** Variables ***
${USER}    ${VALID_PARTNERCRA}
${PASSWORD}    ${VALID_PARTNERCRA_PASSWORD}
${ORGANIZATION_TITLE}   Test organization
${PRODUCT_FAMILY_TITLE}     Test title
${PRODUCT_OFFERING_TITLE}   Test product offering
${SERVICE_OFFERING_TITLE}   Test service offering


*** Test Cases ***      Phone Number
Plain text              phonenumber
#Empty                   ${EMPTY}  #Phone number is not mandatory
Text starting with +    +phone
Text and numbers        +345test
Numbers                 12345678
Number and text         1234abc
PlusNumber and text     +358asd

*** Keywords ***
Add company
    [Arguments]    ${phonenumber}
    Wait Until Page Contains    Add content
    Mouse Over  link=Add content    
    Click Element   link=Organization
    Wait Until Page Contains    Organization title
    Input Text  id=edit-field-page-title-und-0-value    ${ORGANIZATION_TITLE}
    Execute Javascript   CKEDITOR.instances['edit-field-free-text-company-und-0-value'].setData('ORGANIZATION DESCRIPTION');
#    Input Text    id=edit-field-free-text-company-und-0-value    Free text organization
    Input Text  id=edit-field-company-address-und-0-value   Kuopio
    Input Text  id=edit-field-company-webpage-und-0-title   test organization
    Input Text  id=edit-field-company-webpage-und-0-url     http://www.google.com
    Input Text  id=edit-field-admin-name-und-0-value    Test
    Input Text  id=edit-field-admin-phone-und-0-value   ${phonenumber}
    Input Text  id=edit-field-admin-email-und-0-email   test@testemailaddress.com
    Click Element   id=edit-submit
    Page Should Contain    Phone number must start with "+" and must includes only numbers
Log Out and Close Browsers
    Wait Until Page Contains    Log out
    Click Element   link=Log out
    Wait Until Page Contains    Log in
    Close All Browsers
