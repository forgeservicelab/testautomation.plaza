*** Settings ***
Documentation   Test Service offering mandatory fields
Suite Setup    Login as partner with cra 
Suite Teardown    Log out and Close Browsers
Test Template    Add service offering
Resource    ../common/assets/secrets.txt
Resource    ../common/assets/resource.txt
Resource    ../local_resource.txt

*** Variables ***
${USER}    ${VALID_PARTNERCRA}
${PASSWORD}    ${VALID_PARTNERCRA_PASSWORD}
${ORGANIZATION_TITLE}   Test organization
${PRODUCT_FAMILY_TITLE}     Test title
${PRODUCT_OFFERING_TITLE}   Test product offering
${SERVICE_OFFERING_TITLE}   Test service offering


 *** Test Cases ***     Title     Tags      Info        Terms     T2        Price          Contact   Cancel    Validate
Empty Title             ${EMPTY}  Testing   info        terms     t2        100 €uro       auto      cancel    title field is required
Empty Tags              Title     ${EMPTY}  info        terms     t2        100 €uro       auto      cancel    Tags field is required
Empty Info              Title     Testing   ${EMPTY}    terms     t2        100 €uro       auto      cancel    Brief overview description field is required.
Empty Terms and Cond    Title     Testing   info        ${EMPTY}  t2        100 €uro       auto      cancel    Terms overview description field is required.
Empty Terms and Cond2   Title     Testing   info        terms     ${EMPTY}  100 €uro       auto      cancel    Terms overview description field is required.
Empty Price             Title     Testing   info        terms     t2        ${EMPTY}       auto      cancel    Pricing information field is required.
Empty Contact           Title     Testing   info        terms     t2        100 €uro       ${EMPTY}  cancel    Contact person / Address field is required
Empty Cancellation      Title     Testing   info        terms     t2        100 $uro       auto      ${EMPTY}  Cancellation / refund policy field is required.
Price only letters      Title     Testing   info        terms     t2        asd euro       auto      cancel    Pricing information must contain letters and at least one number.
Price only Numbers      Title     Testing   info        terms     t2        100            auto      cancel    Pricing information must contain letters and at least one number.

*** Keywords ***
Add service offering
    [Arguments]    ${title}    ${tags}    ${info}    ${terms}   ${t2}     ${pricing}    ${contact}   ${cancel}    ${validate}
    Wait Until Page Contains    Add content
    Mouse Over  link=Add content
    Wait Until PAge Contains    Service offering
    Click Element   link=Service offering
    Wait Until Page Contains    Offering title
    Input Text  id=edit-field-page-title-und-0-value    ${title}
    Input Text  id=edit-field-offering-tags-und     ${Tags} 
    Input Text  id=edit-field-offering-address-und-0-value    ${contact}
    Execute Javascript   CKEDITOR.instances['edit-field-brief-overview-description-und-0-value'].setData('${info}');
    Click Link  Pricing
    Input Text  id=edit-field-pricing-information-und-0-value    ${pricing}
    Click Link  Terms and conditions
    Execute Javascript   CKEDITOR.instances['edit-field-terms-conditions-inside-und-0-field-terms-and-conditions-text-und-0-value'].setData('${terms}');
    Execute Javascript   CKEDITOR.instances['edit-field-terms-conditions-prod-und-0-field-terms-conditions-text-2-und-0-value'].setData('${t2}');
    Click Element   dom=document.getElementsByName('field_organizer[und]')[0]
    Input Text  id=edit-field-cancellation-refund-policy-und-0-value    ${cancel}
    Click Element    id=edit-submit
    Wait Until Page Contains    ${validate}
