*** Settings ***
Suite Setup    Go to frontpage
Suite Teardown    Close All Browsers
Test Template    Plaza Login with CAS
Resource    ./common/assets/secrets.txt
Resource    ./common/assets/resource.txt
Resource    local_resource.txt

*** Variables ***
${USER}    ${VALID_USER}
${PASSWORD}    ${VALID_PASSWORD}
${TO}    ${TO_XMPP}

*** Test Cases ***    Username                Password
digile staff           ${VALID_USER}   ${VALID_PASSWORD}
service developer      ${VALID_SERVICE_DEVELOPER}   ${VALID_SERVICE_DEVELOPER_PASSWORD}
partner                ${VALID_PARTNER}   ${VALID_PARTNER_PASSWORD}
partner with cra       ${VALID_PARTNERCRA}    ${VALID_PARTNERCRA_PASSWORD}



*** Keywords ***
Plaza Login with CAS
    [Arguments]    ${username}    ${password}
    Go to    ${DRUPAL_URL} 
    Wait Until Page Contains    Log in
    Click Link    Log in
    Set Log Level    NONE
    login Auth    ${username}      ${password}    You are currently logged in as ${username}
    Set Log Level    DEBUG
    Wait Until Page Contains    ${username}
    Click Link    Log out
    Wait Until Page Contains    Log in
    Go to     ${AUTH_URL}
    Click Link    Logout
    Wait Until Page Contains    Bye 
