*** Settings ***
Suite Setup    Go to homepage
Suite Teardown    Close All Browsers
Test Template    Plaza Login with CAS
Resource    ./common/assets/secrets.txt
Resource    ./common/assets/resource.txt
Resource    local_resource.txt

*** Variables ***
${USER}    ${VALID_USER}
${PASSWORD}    ${VALID_PASSWORD}
${TO}    ${TO_XMPP}

*** Test Cases ***    Username                Password
digile staff           ${VALID_USER}   ${VALID_PASSWORD}
service developer      ${VALID_SERVICE_DEVELOPER}   ${VALID_SERVICE_DEVELOPER_PASSWORD}
partner                ${VALID_PARTNER}   ${VALID_PARTNER_PASSWORD}
partner with cra       ${VALID_PARTNERCRA}    ${VALID_PARTNERCRA_PASSWORD}



*** Keywords ***
Plaza Login with CAS
    [Arguments]    ${username}    ${password}
    Go to    ${AUTH_URL} 
    Wait Until Page Contains    Login
    Set Log Level    NONE
    login Auth    ${username}      ${password}    You are currently logged in as ${username}
    Set Log Level    DEBUG
    Go to    ${DRUPAL_URL}/en/cas?destination=node/11
    Wait Until Page Contains    ${username}
    Go to    ${AUTH_URL}
    Wait Until Page Contains    Hello
    Go to    ${AUTH_URL}/logout
    Wait Until Page Contains    Bye.
    Go to    ${DRUPAL_URL}
    Wait Until Page Contains    ${username}
    Go to    ${DRUPAL_URL}/en/user/logout
    Wait Until Page Contains    Log in

