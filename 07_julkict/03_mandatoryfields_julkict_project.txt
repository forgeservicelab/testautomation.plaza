*** Settings ***
Documentation   Check mandatoryfields in julkICT service offer creation
Suite Setup    Login as julkict
Suite Teardown    Log out and Close
Test Template    Add free of charge offering
Resource    ../common/assets/secrets.txt
Resource    ../common/assets/resource.txt
Resource    ../local_resource.txt

*** Variables ***
${USER}    ${VALID_JULKICT}
${PASSWORD}    ${VALID_JULKICT_PASSWORD}

*** Test Cases ***    Title     Tags      Overview    Price     Terms     Terms2    Validation
Empty title           ${EMPTY}  Testing   Overview    100 €uro  Terms     Terms2    Offering title field is required.
Empty tags            julkict   ${EMPTY}  Overview    100 €uro  Terms     Terms2    Tags field is required.
Empty overview        julkict   Testing   ${EMPTY}    100 €uro  Terms     Terms2    Brief overview description field is required.
Empty Price           julkict   Testing   Overview    ${EMPTY}  Terms     Terms2    Pricing information field is required.
Empty Terms1          julkict   Testing   Overview    100 €uro  ${EMPTY}  Terms2    Terms overview description field is required.
Empty Terms2          julkict   Testing   Overview    100 €uro  Terms     ${EMPTY}  Terms overview description field is required.


*** Keywords ***
Add free of charge offering
    [Arguments]    ${title}    ${tags}    ${overview}    ${price}    ${terms1}    ${terms2}    ${validate}
    Wait Until Page Contains    Add content
    Mouse Over  link=Add content
    Wait Until Page Contains    Free of charge
    Click Element   link=Free of charge offering
    Wait Until Page Contains    Offering title
    Input Text  id=edit-field-page-title-und-0-value    ${title}
    Input Text  id=edit-field-offering-tags-und     ${tags}
    Execute Javascript   CKEDITOR.instances['edit-field-brief-overview-description-und-0-value'].setData('${overview}');
    Click Element    id=edit-field-this-is-julkict-lab-projec-und-1
    Click Element    id=edit-field-this-project-is-free-of-ch-und-1
    Click Link  Pricing
    Input Text  id=edit-field-pricing-information-und-0-value   ${price}
    Click Link  Terms and conditions
    Execute Javascript   CKEDITOR.instances['edit-field-terms-conditions-inside-und-0-field-terms-and-conditions-text-und-0-value'].setData('${terms1}');
    Execute Javascript   CKEDITOR.instances['edit-field-terms-conditions-prod-und-0-field-terms-conditions-text-2-und-0-value'].setData('${terms2}');
#    Input Text    id=edit-field-cancellation-refund-policy-und-0-value    Cancellation policy
#    Input Text  id=edit-field-live-demo-url-und-0-title     Test url title
#    Input Text  id=edit-field-live-demo-url-und-0-url   http://www.google.com
#    Click Element   dom=document.getElementsByName('field_organizer[und]')[0]
#    Input Text  id=edit-field-offering-address-und-0-value  Test Person, Testroad 5
#    Input Text  id=edit-field-offering-phone-und-0-value    +358400123456
#    Input Text  id=edit-field-offering-email-und-0-email    test@testemailaddress.com
#    Input Text  id=edit-field-offering-website-und-0-url    http://www.google.com
    Click Element   id=edit-submit
    Page Should Not Contain    has been created.
    Page Should Contain    ${validate}

Log Out and Close
    Wait Until Page Contains    Log out
    Click link    Log out
    Wait Until Page Contains    Log in
    Go to    ${AUTH_URL}
    Click Link    Logout
    Page Should Contain    Bye
    Close All Browsers

